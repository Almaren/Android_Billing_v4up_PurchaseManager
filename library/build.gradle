plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

def localProperties = new Properties()
try {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (Exception e) {
    logger.warn("No Local Properties File Found!")
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            aidl.srcDirs = ['src/main/kotlin']
            renderscript.srcDirs = ['src/main/kotlin']
        }
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        consumerProguardFiles "consumer-rules.pro"

        if (!localProperties['base64EncodedPublicKey']) {
            logger.warn('base64EncodedPublicKey must be set for signature verification. ' +
                    'You can add it to local.properties once you have gotten it from Play. ' +
                    'Example: base64EncodedPublicKey=[key value]')
        }
        buildConfigField("String", "BASE64_ENCODED_PUBLIC_KEY", "\"" + localProperties['base64EncodedPublicKey'] + "\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    implementation 'com.android.billingclient:billing-ktx:4.0.0'
}
